********************************************************************
*  SCOTT SMOKE
*  CS310 SPRING
*  THIS PROGRAM WILL READ IN 30 OR LESS VALUES STORE THEM IN A TABLE
*  THEN USE SELECTION SORT ALGORITHM TO SORT THE LIST IN DECENDING
*  ORDER.
*  REGISTRY KEY:
*    11 BRANCH CONTROL
*    12 BASE REGISTER
********************************************************************
MAIN	START
	STM	14,12,12(13)
	BALR	12,0
	USING	HERE,12
HERE	ST	13,SAVEAREA+4
	LA	13,SAVEAREA
	LA	1,PARAMLST
	L	15,=V(READ)
	BALR	14,15
	LA	1,PARAMLST
	L	15,=V(PRINT)
	BALR	14,15
	XPRNT	CARAIGE,1
	LA	1,PARAMLST
	L	15,=V(SELSORT)
	BALR	14,15
	LA	1,PARAMLST
	L	15,=V(PRINT)
	BALR	14,15
	L	13,SAVEAREA+4
	LM	14,12,12(13)
	BR	14
SAVEAREA	DS	18F
TABLE	DS	30F
TEND	DS	0F
PTRTEND	DS	1F
PARAMLST	DC	A(TABLE)
	DC	A(PTRTEND)
	DC	A(TEND)
CARAIGE	DC	C'1'
	LTORG
	EJECT
***************************************
*  THIS SUBROUTINE WILL READ IN 30 OR 
*  LESS VALUES AND CREATE A TABLE.
*  REGISTER KEY:
*     2  POINTER TO START OF TABLE
*     8   STEPSIZE
*     9  POINTER TO TABLE END
*     5  TEMPORARY HOLDING	
**************************************
READ	CSECT
	STM	14,12,12(13)
	BALR	12,0
	USING	READHERE,12
READHERE	ST	13,READAREA+4
	LA	13,READAREA
	L	2,0(1)
	L	9,8(1)
	L	10,4(1)
	LA	8,4
READLOOP	XREAD	CARD,80
	BC	B'0100',ENDREAD	EOF CHECK
	XDECI	5,CARD
	ST	5,0(2)
	ST	2,0(10)	STORES THE ADDRESS OF THE LAST ITEM
	BXLE	2,8,READLOOP
ENDREAD	L	13,READAREA+4
	LM	14,12,12(13)
	BR	14
READAREA	DS	18F
CARD	DS	CL80
	LTORG
	EJECT	
**************************************
*  THIS SUBROUTINE WILL PRINT THE 
*  TABLE.
*    REGISTER KEY:
*       2 POINTER TO START OF TABLE
*       8 STEPSIZE
*       9 POINTER TO TABLE END
*       5 TEMPORARY HOLDING
**************************************
PRINT	CSECT
	STM	14,12,12(13)
	BALR	12,0
	USING	PRNTHERE,12
PRNTHERE	ST	13,PRNTAREA+4
	LA	13,PRNTAREA
	L	2,0(1)	
	L	9,4(1)
	L	9,0(9)
	LA	8,4
PRNTLOOP	L	5,0(2)
	XDECO	5,OUT
	XPRNT	CC,13
	BXLE	2,8,PRNTLOOP
	L	13,PRNTAREA+4
	LM	14,12,12(13)
	BR	14 
PRNTAREA	DS	18F
CC	DC	C'0'
OUT	DS	CL12
	LTORG
	EJECT
**************************************
*  THIS SUBROUTINE WILL PERFORM A 
*  SELECTION SORT ON THE TABLE SORTING
*  IN DECENDING ORDER.
*    REGISTER KEY:
*       2 POINTER TO OUT SEARCH VALUE
*       5 POINTER TO INNER SEARCH VALUE
*      10 HOLDS LARGEST VALUE
*       3 HOLDS ADDRESS TO OUTER VALUE
*       9 POINTER TO TABLE END
*       8 STEPSIZE
*       4 TEMP HOLDING FOR SWAP	
**************************************
SELSORT	CSECT
	STM	14,12,12(13)
	BALR	12,0
	USING	OVERHERE,12
OVERHERE	ST	13,LOCAREA+4
	LA	13,LOCAREA
	L	2,0(1)	
	L	9,4(1)
	L	9,0(9)	ADDRESS OF LAST VALUE
	LA	8,4
OUTLOOP	L	10,0(2)	SETS LARGEST
	LR	3,2	POINTER TO LARGEST	
	LA	5,4(2)	POINTER TO BEGINNING OF INNERLOOP
INLOOP	C	10,0(5)	
	BC	B'1010',ENDIF
	LR	3,5	POINTER TO LARGESST
	L	10,0(5)	LARGEST VALUE
ENDIF	BXLE	5,8,INLOOP
	L	4,0(2)	TEMP HOLDING
	ST	10,0(2)
	ST	4,0(3)
	BXLE	2,8,OUTLOOP
	L	13,LOCAREA+4
	LM	14,12,12(13)
	BR	14
LOCAREA	DS	18F
	LTORG
	END	MAIN
$ENTRY TEST.DAT
