********************************************************************
* SCOTT SMOKE
* CS 310
* ASSIGNEMENT 5
* SPING 2013
* THIS PROGRAM WILL READ IN DATA LINE UNTIL END OF FILE, CHECK THE
* TRANSACTION CODE AND EITHER SET THE INITIAL BALANCE, WITHDRAW, OR
* DEPOSIT MONEY INTO A BANKING ACCOUNT, AND WILL PRINT A ACCOUNT 
* STATEMENT NOTING THE CHANGES.
* REGISTER KEY:
*	11 BRANCH AND LINK
*	12 BASE REGISTER
*	13 POINTER TO SAVE REGION
*  	14 BRANCH AND RETURN TO CONTROLLER 
*
********************************************************************
MAIN	START
	STM	14,12,12(13)
	BALR	12,0
	USING HERE,12
HERE	ST	13,SAVEAREA+4
	LA	13,SAVEAREA
	SPACE
	BAL	11,CLEAR INITIALIZATION OF VARIABLES
	ZAP	COUNT(3),=P'0'
	SPACE
	XPRNT	TITLE,36
	XPRNT	LABELS,47
READ	XREAD	TRANCODE,10
	BC	B'0100',ENDREAD
	CLI	TRANCODE,C'0'
	BC	B'0111',ELSEIF
	CP	COUNT,=P'0'
	BC	B'1000',INIACC
	BAL	11,CALCAVGS
	BAL	11,PRINTDEP
	BAL	11,PRINTWIT
	BAL	11,CLEAR
	XPRNT	TITLE,36
	XPRNT	LABELS,47
INIACC	BAL	11,INITIALB
	AP	COUNT,=P'1'
ELSEIF	CLI	TRANCODE,C'1'
	BC	B'0111',ELSEIF1
	BAL	11,PUT
ELSEIF1	CLI	TRANCODE,C'2'
	BC	B'0111',ELSE
	BAL	11,TAKE
ELSE	CLI	TRANCODE,C'3'
	BC	B'0111',ENDIF
	BAL	11,CALCINT
ENDIF 	BC	B'1111',READ
ENDREAD	BAL	11,CALCAVGS
	BAL	11,PRINTDEP
	BAL	11,PRINTWIT
	SPACE
	L	13,SAVEAREA+4
	LM	14,12,12(13)
	BR	14
	SPACE
SAVEAREA	DS	18F
TRANCODE	DS	CL10
COUNT	DS	PL3
DEPOSIT	DS	PL5
WITHDRAW	DS	PL5
BALANCE	DS	PL5
TEMP	DS	PL5     HOLDING VALUE FOR FORMATP
MINBAL	DS	PL7
RATE	DS	PL3
TOTALD	DS	PL6   RUNNING TOTAL OF DEPOSITS
TOTALW	DS	PL6   RUNNING TOTAL OF WITHDRAWS
NUMOFDEP	DS	PL5   TOTAL NUMBER OF DEPOSITS
NUMOFWIT	DS	PL5   TOTAL NUMBER OF WITHDRAWS
AVGDEP	DS	0PL10
QUODEP	DS	PL5
REMDEP	DS	PL5
AVGWIT	DS	0PL10
QUOWIT	DS	PL5
REMWIT	DS	PL5
PLINE	DS	CL50
TITLE	DC	CL36'0           SAVINGS ACCOUNT REPORT '
LABELS	DC	CL46'  TRAN CODE  AMOUNT      RATE  CURRENT BALANCE'
	EJECT
***************************************
* THIS SUBROUTINE WILL SET AND PRINT 
* THE INITIAL BALANCE.
***************************************
INITIALB	LR	10,11
	PACK	BALANCE(5),TRANCODE+1(9)
	MVC	PLINE+11(13),=X'40206B2020206B2021204B2020'
	ED	PLINE+11(13),BALANCE
	ZAP	TEMP(5),BALANCE(5)
	ZAP	MINBAL(7),BALANCE(5)
	BAL	11,FORMATP
	LR	11,10
	BR	11
***************************************
* THIS SUBROUTINE WILL ADD THE DEPOSIT
* TO THE BALANCE AND KEEP A RUNNING 
* TOTAL OF DEPOSITS AND NUMBER OF 
* DEPOSITS.
***************************************
PUT	LR	10,11
	PACK	DEPOSIT(5),TRANCODE+1(9)
	AP	BALANCE(5),DEPOSIT(5)
	BAL	11,COMPBAL
	AP	TOTALD(6),DEPOSIT(5)
	AP	NUMOFDEP(5),=P'1'
	ZAP	TEMP(5),DEPOSIT(5)
	BAL	11,FORMATP
	LR	11,10
	BR	11
	EJECT
***************************************
* THIS SUBROUTINE WILL CALCULATE THE
* AVERAGE DEPOSIT.
***************************************
CALCAVGS	LR	10,11
	CP	NUMOFDEP(5),=P'0'
	BC	B'1000',SKIP
	ZAP	AVGDEP(10),TOTALD(6)
	DP	AVGDEP(10),NUMOFDEP(5)
	AP	REMDEP(5),REMDEP(5)
	CP	REMDEP(5),NUMOFDEP(5)
	BC	B'0100',SKIP
	AP	QUODEP(5),=P'1'
SKIP	CP	NUMOFWIT(5),=P'0'
	BC	B'1000',SKIP1
	ZAP	AVGWIT(10),TOTALW(6)
	DP	AVGWIT(10),NUMOFWIT(5)
	AP	REMWIT(5),REMWIT(5)
	CP	REMWIT(5),NUMOFWIT(5)
	BC	B'0100',SKIP1
	AP	QUOWIT(5),=P'1'
SKIP1	BR	11
	EJECT
***************************************
* THIS SUBROUTINE WILL SUBTRACT THE
* WITHDRAW FROM THE BALANCE, KEEP A
* RUNNING TOTAL OF WITHDRAWS AND THE
* NUMBER OF WITHDRAWS.
***************************************
TAKE	LR	10,11
	PACK	WITHDRAW(5),TRANCODE+1(9)
	SP	BALANCE(5),WITHDRAW(5)
	BAL	11,COMPBAL
	AP	TOTALW(6),WITHDRAW(5)
	AP	NUMOFWIT(5),=P'1'
	ZAP	TEMP(5),WITHDRAW(5)
	BAL	11,FORMATP
	LR	11,10
	BR	11
***************************************
* THIS SUBROUTINE WILL COMPARE THE 
* BALANCE TO THE MINIMUM BALANCE AND 
* SET MINIMUM IF I IS GREATER THAN
* BALANCE.
***************************************
COMPBAL	CP	BALANCE(5),MINBAL(7)
	BC	B'1010',ENDCOMP
	ZAP	MINBAL(7),BALANCE(5)
ENDCOMP	BR	11
	EJECT
***************************************
* THIS SUBROUTINE WILL CALCULATE THE 
* INTEREST AND ADD IT BACK TO THE 
* BALANCE AND SET THE MINIMUM.
***************************************
CALCINT	PACK	RATE(3),TRANCODE+1(3)
	ZAP	INTEREST(10),=P'0'
	CP	MINBAL(7),=P'0'
	BC	B'0100',ENDCAL
	MP	MINBAL(7),RATE(3)
	ZAP	INTEREST(10),MINBAL(7)
	DP	INTEREST(10),=P'10000'
	CP	REMINT(3),=P'5000'
	BC	B'0100',ENDCAL1
	AP	QUOINT(7),=P'1'
ENDCAL1	AP	BALANCE(5),QUOINT(7)
ENDCAL	ZAP	MINBAL(7),BALANCE(5)
	MVI	PLINE,C' '
	MVC	PLINE+1(50),PLINE
	MVC	PLINE+1(1),TRANCODE
	MVC	PLINE+22(7),=X'402021204B2020'
	ED	PLINE+22(7),RATE
	MVC	PLINE+31(14),=X'40206B2020206B2021204B202060'
	ED	PLINE+31(14),BALANCE
	XPRNT	PLINE,50
	BR	11
INTEREST DS	0PL10
QUOINT	DS	PL7
REMINT	DS	PL3
	EJECT
***************************************
* THIS SUBROUTINE WILL PRINT A DEPOSIT
* STATEMENT LISTING THE TOTAL AMOUNT
* OF DEPOSITS, TOTAL DEPOSITS MADE, AND
* AVERAGE DEPOSITS.
***************************************
PRINTDEP MVI	PLINE,C' '
	MVC 	PLINE+1(50),PLINE
	MVC	PLINE(14),=CL14' # DEPOSITS = '
	MVC	PLINE+14(10),=X'40202020202020202120'
	ED	PLINE+14(10),NUMOFDEP
	XPRNT	PLINE,50
	MVC	PLINE(27),=CL27' TOTAL AMOUNT OF DEPOSITS='
	MVC	PLINE+27(15),=X'402020206B2020206B2021204B2020'
	EDMK	PLINE+27(15),TOTALD
	BCTR	1,0
	MVI	0(1),C'$'
	XPRNT	PLINE,42
	MVI	PLINE,C' '
	MVC	PLINE+1(50),PLINE
	MVC	PLINE(25),=CL25' AVG. AMOUNT/DEPOSIT       = '
	MVC	PLINE+25(13),=X'40206B2020206B2021204B2020'
	EDMK	PLINE+25(13),QUODEP
	BCTR	1,0
	MVI	0(1),C'$'
	XPRNT	PLINE,50
	BR 	11
	EJECT	
***************************************
* THIS SUBROUTINE WILL PRINT THE TOTAL
* NUMBER OF WITHDRAWS, TOTAL WITHDRAWS
* MADE, AND THE AVERAGE WITHDRAW.
***************************************
PRINTWIT	MVI	PLINE,C' '
	MVC	PLINE+1(50),PLINE
	MVC	PLINE(16),=C' # WITHDRAWLS = '
	MVC	PLINE+16(10),=X'40202020202020202120'
	ED	PLINE+16(10),NUMOFWIT
	XPRNT	PLINE,50
	MVC	PLINE(31),=CL31' TOTAL AMOUNT OF WITHDRAWLS = '
	MVC	PLINE+31(15),=X'402020206B2020206B2021204B2020'
	EDMK	PLINE+31(15),TOTALW
	BCTR	1,0
	MVI	0(1),C'$'
	XPRNT	PLINE,50
	MVI	PLINE,C' '
	MVC	PLINE+1(50),PLINE
	MVC	PLINE(26),=CL26' AVG. AMOUNT/WITHDRAWLS = '
	MVC	PLINE+26(13),=X'40206B2020206B2020204B2020'
	EDMK	PLINE+26(13),QUOWIT
	BCTR	1,0
	MVI	0(1),C'$'
	XPRNT	PLINE,50
	BR	11
	EJECT
***************************************
* THIS SUBROUTINE WILL FORMAT THE PRINT 
* LINE AND PRINT IT.
***************************************
FORMATP 	MVI	PLINE,C' '
	MVC	PLINE+1(50),PLINE  CLEARS PLINE
	MVC	PLINE+1(1),TRANCODE
	MVI	PLINE+2,C' '
	MVC	PLINE+3(9),PLINE+2
	MVC	PLINE+11(14),=X'40206B2020206B2021204B202060'
	ED	PLINE+11(14),TEMP
	MVI	PLINE+25,C' '
	MVC	PLINE+26(5),PLINE+25
	MVC	PLINE+31(14),=X'40206B2020206B2021204B202060'
	ED	PLINE+31(14),BALANCE
	MVI	PLINE+45,C' '
	MVC	PLINE+46(2),PLINE+45
	XPRNT	PLINE,50
	BR	11
***************************************
* THIS SUBROUTINE WILL CLEAR THE FIELDS
***************************************
CLEAR	ZAP	TOTALD,=P'0'
	ZAP	TOTALW,=P'0'
	ZAP	NUMOFDEP,=P'0'
	ZAP	NUMOFWIT,=P'0'
	PACK	MINBAL(5),=P'0'
	ZAP	RATE,=P'0'
	ZAP	AVGDEP(10),=P'0'
	ZAP	AVGWIT(10),=P'0'
	ZAP	TEMP(5),=P'0'
	BR	11
	END	MAIN
$ENTRY
0000002000
3555
1000001000
3555
2000001000
3555
0000010000
3555
1000010000
2000010000