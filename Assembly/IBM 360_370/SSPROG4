********************************************************************
*  SCOTT SMOKE
*  CS310 SPRING
*  THIS PROGRAM WILL READ IN AN UNKNOWN NUMBER OF LINES THAT WILL
*  CONTAINS A PERSONS NAMES AND ADRESS. THIS WILL REFORMAT THE USER
*  INPUTED DATA FROM 5 LINES TO THREE AND PRINT IT TO THE SCREEN.
*
*
********************************************************************
MAIN	START
	STM	14,12,12(13)
	BALR	12,0
	USING	HERE,12
HERE	ST	13,SAVEAREA+4
	LA	13,SAVEAREA
READ	XREAD	CARD,50	GETS NAME
	BC	B'0100',ENDREAD
	BAL	11,FNAME
	BAL	11,NAMEPRNT
	XREAD	CARD,50	GETS ADDRESS
	BAL	11,ADDRESS
	XREAD	CITY,50
	XREAD	STATE,50
	XREAD	ZIP,50
	BAL	11,CONCAT
	LA	4,PLINE
	BAL	11,CASC	PINTS BLANK LINE
	XPRNT	PLINE,50
	BC	B'1111',READ
ENDREAD	L	13,SAVEAREA+4
	LM	14,12,12(13)
	BR	14
SAVEAREA	DS	18F
CARD	DS	CL50
CITY	DS	CL50
STATE	DS	CL50
ZIP	DS	CL50
PLINE	DS	CL51
***************************************
* THIS SUBROUTINE WILL TAKE THE FIRST 
* READ IN LINE(NAME) AND FIND THE 
* BLANKS
* REGISTER KEY:
*	2  CONTROL
*	4  STEPSIZE
*	5  TERMINAL
*	6  POINTER TO FIRST NAME
*	7  POINTER TO MIDDLE
*	8  POINTER TO LAST	
*
*************************************** 
FNAME	LA	2,CARD
	LA	4,1
	LA	5,CARD+49
	SR	3,3
	LR	6,2
SEARCHN	CLI	0(2),C' '
	BC	B'0111',ENDIF	IF NOT A BLANK
	LA	3,1(3)	KEEPS TRACK OF BLANKS ENCOUNTERED
	C	3,=F'1'
	BC	B'0111',NEXT
	LR	9,2
	LA	7,1(9)
NEXT	C	3,=F'2'
	BC	B'0111',ENDIF
	LR	9,2
	LA	8,1(9)	SETS POINTER TO LAST
ENDIF	BXLE	2,4,SEARCHN
	BR	11
***************************************
*  THIS SUBROUTINE WILL REFORMAT THE 
*  FIRST LINE ENTERED BY THE USER
*  AND ENTER IT INTO THE PRINT BUFFER
*  REGISTER KEY:
*	4 POINTER TO PRINT LINE
*	6 POINTER TO FIRST NAME
*	7 POINTER TO MIDDLE NAME
*	8 POINTER TO LAST NAME	
***************************************
NAMEPRNT	LA	4,PLINE	LINE TO BE PRINTED
	LR	10,11
	LR	2,8
	BAL	11,PLINEBLD
 	LA	4,1(4)
	MVI	0(4),C','
	LA	4,1(4)
	MVI	0(4),C' '
	LR	2,6
	BAL	11,PLINEBLD
	LA	4,1(4)
	MVI	0(4),C' '
	MVC	1(1,4),0(7)	PUTS MIDDLE INITIAL INTO PLINE
	LA	4,2(4)
	BAL	11,CASC
	XPRNT	PLINE,50
	LR	11,10
	BR	11
***************************************
* THIS SUBROUTINE WILL COPY THE ADDRESS
* FROM CARD TO PLINE AND THEN PRINT
* PLINE.
***************************************
ADDRESS	MVI	PLINE,C' '
	MVC	PLINE+1(49),CARD
	XPRNT	PLINE,50
	BR	11
***************************************
* THIS SUBROUTINE WILL COMBINE THE 
* LINES THAT CONTAIN CITY, STATE AND
* ZIP INTO ONE LINE AND PRINT IT.
*
*
***************************************
CONCAT	LR	10,11
	LA	4,PLINE
	LA	2,CITY
	BAL	11,PLINEBLD
	LA	4,1(4)
	MVI	0(4),C','
	LA	4,1(4)
	LA	2,STATE
	BAL	11,PLINEBLD
	LA	4,1(4)
	MVI	0(4),C' '
	LA	2,ZIP
	BAL	11,PLINEBLD
	LA	4,1(4)
	MVI	0(4),C' '
	BAL	11,CASC
	XPRNT	PLINE,50
	LR	11,10
	BR	11	
***************************************
* THIS WILL ADD A STRING TO THE PRINT
* LINE
***************************************
PLINEBLD	MVI	0(4),C' '
LOOP	CLI	0(2),C' '
	BC	B'1000',ENDLOOP
	LA	4,1(4)
	MVC	0(1,4),0(2)
	LA	2,1(2)
	BC	B'1111',LOOP
ENDLOOP	BR	11
***************************************
* THIS SUBROUTINE WILL CASCADE A BLANK
* AT THE END OF THE PRINT LINE
*
***************************************
CASC	LA	8,1
	LA	9,PLINE+49
CASCLOOP	MVI	0(4),C' '
	BXLE	4,8,CASCLOOP
	BR	11
	END	MAIN
$ENTRY
JOHN JAMESON SMITH
103 PARK VIEW AVENUE APARTMENT 138
FLORENCE 
AL
12345
